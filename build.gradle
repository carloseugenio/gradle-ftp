plugins {
    id "com.jfrog.bintray" version "1.8.4"
    // Apply the groovy plugin to add support for Groovy
    id 'groovy'
	// This gives problems with SF4J dependencies bundled in API
    //id 'java-gradle-plugin'
    id 'ivy-publish'
    id "maven-publish"
	id 'java-gradle-plugin'
	id 'idea'
}

repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
	// For the Gradle tooling api
	maven {
		url 'https://repo.gradle.org/gradle/repo'
	}
}

dependencies {
	//compileOnly gradleApi()
	implementation gradleApi()

    // Use the latest Groovy version for building this library
    implementation 'org.codehaus.groovy:groovy-all:2.5.6'
    // FTP Client library
    implementation 'commons-net:commons-net:3.3'

	testImplementation gradleTestKit()

    // Use the awesome Spock testing and specification framework
    //testImplementation 'org.spockframework:spock-core:1.2-groovy-2.5'
	// Mock the FTP Server
	testImplementation 'org.mockftpserver:MockFtpServer:2.7.1'

	testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "carloseugenio"
            name "Carlos Eugenio P Purificacao"
            email "carloseugenio@gmail.com"
        }
    }

    scm {
        url "https://github.com/carloseugenio/gradle-ftp"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'br.net.carloseugenio.gradle'
            artifactId 'ftp'
            version '0.0.1'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A gradle plugin for interacting with FTP servers.')
                root.appendNode('name', 'Gradle FTP Plugin')
                root.appendNode('url', 'https://github.com/carloseugenio/gradle-ftp')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
	user = System.getProperty('bintray.user')
	key = System.getProperty('bintray.key')
	publications = ['mavenPublication']

	pkg {
		repo = 'gradle-ftp'
		name = 'gradle-ftp'
		userOrg = 'gradle-ftp'
		licenses = ['Apache-2.0']
		vcsUrl = 'https://github.com/carloseugenio/gradle-ftp'
		version {
			name = '0.0.1'
			desc = '0.0.1'
			released  = new Date()
		}
	}
}

